apply plugin: "maven-publish"
apply plugin: 'signing'

publishing {
    publications {
        Interstellar(MavenPublication) {
            groupId = project.group
            version = project.version
            artifactId = project.archivesBaseName.toString().toLowerCase()

            artifact deobfJar
            artifact sourceJar
            artifact javadocJar

            pom {
                name = project.archivesBaseName
                packaging = 'jar'
                description = 'Core Library & API'
                url = 'https://github.com/ReadOnlyDevelopment/Interstellar'
    
                licenses {
                    license {
                        name = 'MIT'
                        url = 'https://raw.githubusercontent.com/ReadOnlyDevelopment/Interstellar/master/LICENSE'
                    }
                }
                
                scm {
                    url = 'https://github.com/ReadOnlyDevelopment/Interstellar'
                    connection = 'scm:git:git://github.com/ReadOnlyDevelopment/Interstellar.git'
                    developerConnection = 'scm:git:git@github.com:ReadOnlyDevelopment/Interstellar.git'
                }
    
                issueManagement {
                    system = 'github'
                    url = 'https://github.com/ReadOnlyDevelopment/Interstellar/issues'
                }
    
                developers {
                    developer {
                        id = 'romvoid95'
                        name = 'rom'
                    }
                }
            }
        }
    }
    
    repositories {
        mavenLocal()
        maven {
            name 'Releases'
            url 'https://maven.readonlydev.com/releases'
            credentials {
                username = "${REPO_USER}"
                password = "${REPO_TOKEN}"
            }
        }
    }
    
    signing {
        publishing.publications.each {
            sign it
        }
    }
}
